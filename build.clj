#!/usr/bin/env bb

(ns build
  "A way to build more plugins for scittle"
  (:require [babashka.fs :as fs]
            [clojure.edn :as edn]
            [clojure.pprint :as pprint]
            [clojure.string :as str]
            [hiccup.core :as hiccup]))

(def plugins
  (edn/read-string (slurp "plugin-templates.edn")))

(defn pretty-spit [f x]
  (spit (str f)
        (with-out-str (binding [*print-namespace-maps* false]
                        (clojure.pprint/pprint x))))
  (println "scittle-kitchen build created" (str f)))

(defn plugin-edn [nm {:keys [namespaces depends-on]}]
  (let [module (str "scittle." nm)]
    [{:name (symbol "scittle" nm)
      :namespaces namespaces
      :js (str "./scittle." nm ".js")
      :shadow-config {:modules {(keyword module) {:init-fn (symbol module "init")
                                                  :depends-on (into #{:scittle} depends-on)
                                                  :entries namespaces}}}}]))

(defn write-plugin [k plugin]
  (let [nm (name k)
        plugin-dir (fs/path "plugins" nm)
        src-dir (fs/path plugin-dir "src" "scittle")
        cljs-file (fs/path src-dir (str nm ".cljs"))
        edn-file (fs/path plugin-dir "src" "scittle_plugin.edn")
        deps-file (fs/path plugin-dir "deps.edn")
        ns-requires (str/join
                     "\n    "
                     (for [ns (:namespaces plugin)]
                       (str "[" ns "]")))
        config-map (str "{:namespaces\n   {"
                        (str/join
                         "\n    "
                         (for [ns (:namespaces plugin)]
                           (str "'" ns " (sci/copy-ns " ns " (sci/create-ns '" ns " nil))")))
                        "}}")]
    (fs/create-dirs src-dir)
    (spit (str cljs-file)
          (str "(ns scittle." nm "\n"
               "  {:no-doc true}\n"
               "  (:require [scittle.core :as scittle]\n"
               "            [sci.core :as sci]"
               (if (seq (:namespaces plugin))
                 (str "\n    " ns-requires)
                 "")
               "))\n\n"
               ";; Plugin: " nm "\n"
               ";; Generated by build.clj\n\n"
               "(defn init []\n"
               "  (scittle/register-plugin!\n"
               "   ::" nm "\n"
               "   " config-map "\n"
               "   ))\n"))
    (println "scittle-kitchen build created" (str cljs-file))
    (pretty-spit edn-file (plugin-edn nm plugin))
    (pretty-spit deps-file {:deps (or (:deps plugin) {})})))

(fs/create-dirs (fs/path "plugins"))
(doseq [[k plugin] plugins]
  (write-plugin k plugin))

(defn find-plugins
  "Not all plugins are generated, so look for them in the plugin directories"
  [base]
  (->> (fs/list-dir base fs/directory?)
       (map fs/file-name)
       (remove #{"demo"})
       (map (fn [name]
              [(keyword name) (fs/path base name)]))
       (sort)))

(defn official-plugins []
  (find-plugins "scittle/plugins"))

(defn kitchen-plugins []
  (find-plugins "plugins"))

(def all-plugins
  (into {} (concat (official-plugins) (kitchen-plugins))))

(defn standard-plugins []
  (let [plugin-dir (fs/path "scittle" "src" "scittle")
        plugin-files (fs/list-dir plugin-dir (complement fs/directory?))]
    (->> (map (comp fs/file-name fs/strip-ext) plugin-files)
         (remove #{"core"})
         (sort))))

(defn generate-index-html [build]
  (let [public-dir (fs/path build "resources" "public")
        index-file (fs/path public-dir "index.html")
        base-url "https://timothypratley.github.io/scittle-kitchen/js/"
        ;; Standard plugins (from scittle/src/scittle/*.cljs)
        std-urls (for [nm (standard-plugins)]
                   (str base-url "scittle." nm ".js"))
        ;; Official plugins (from scittle/plugins/)
        official-urls (for [[k _] (official-plugins)]
                        (str base-url "scittle." (name k) ".js"))
        ;; Community plugins (from plugins/)
        community-urls (for [[k _] (kitchen-plugins)]
                         (str base-url "scittle." (name k) ".js"))
        page [:html
              [:head
               [:meta {:charset "utf-8"}]
               [:meta {:name "viewport" :content "width=device-width, initial-scale=1.0"}]
               [:title "Scittle Kitchen Plugins"]
               (for [url (concat std-urls official-urls community-urls)]
                 [:script {:src url}])]
              [:body
               [:h1 "Scittle Kitchen"]
               [:p "Must be included before plugins"]
               [:div [:a {:href (str base-url "scittle.js")} (str base-url "scittle.js")]]
               [:h2 "Standard Plugins (built-in)"]
               (for [url std-urls]
                 [:div [:a {:href url} url]])
               [:h2 "Official Plugins (from scittle/plugins/)"]
               (for [url official-urls]
                 [:div [:a {:href url} url]])
               [:h2 "Community Plugins (from plugins/)"]
               (for [url community-urls]
                 [:div [:a {:href url} url]])]]]
    (fs/create-dirs public-dir)
    (spit (str index-file)
          (str "<!DOCTYPE html>" \newline
               (hiccup/html page)))
    (println "scittle-kitchen build created" (str index-file))))

(defn scittle-sci-version
  "Otherwise there will be a version conflict"
  []
  (get-in (edn/read-string (slurp "scittle/deps.edn"))
          [:deps 'org.babashka/sci]))

(defn local [build path]
  {:local/root (str (fs/relativize build path))})

(defn make
  "Generate a deps.edn in ./<build-name>/ with plugins, scittle, and sci on the classpath."
  ([] (make (keys all-plugins) "all"))
  ([plugins build]
   (fs/create-dirs build)
   (let [scittle-deps {'io.github.babashka/scittle (local build "scittle")
                       'io.github.babashka/scittle.build (local build (fs/path "scittle" "build"))
                       'org.babashka/sci (scittle-sci-version)}
         plugin-deps (map (fn [plugin]
                            [(symbol "scittle-kitchen.plugins" (str "scittle." (name plugin)))
                             (local build (get all-plugins plugin))])
                          plugins)
         deps (into scittle-deps plugin-deps)]
     (pretty-spit (fs/path build "bb.edn")
                  {:deps deps
                   :tasks '{:requires ([scittle.build :as build])
                            release {:task (scittle.build/build {})}}})
     (pretty-spit (fs/path build "deps.edn")
                  {:deps scittle-deps})
  (generate-index-html build))))


;; Allow building a single plugin by name via command line argument
(let [args *command-line-args*]
  (if (seq args)
    (let [plugin-key (keyword (first args))
          plugin (get all-plugins plugin-key)]
      (if plugin
        (make [plugin-key] (name plugin-key))
        (do
          (println "Unknown plugin:" (first args))
          (println "Available plugins:" (str/join ", " (map name (keys all-plugins))))
          (System/exit 1))))
    (make)))
